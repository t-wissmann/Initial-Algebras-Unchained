{-# OPTIONS --without-K #-}
open import Categories.Category
open import Categories.Functor using (Functor; Endofunctor)

module recursive-coalgebra {o â„“ e} (C : Category o â„“ e) (F : Endofunctor C) where

private
  module C = Category C

open import Level

open import Categories.Functor.Coalgebra
open import Categories.Functor.Algebra hiding (iterate)
open import Categories.Category using (Category)
open import Categories.Category.Construction.F-Algebras
open import Categories.Category.Construction.F-Coalgebras
open import Categories.Morphism using (IsIso; Iso; module â‰…; Retract)
import Categories.Morphism
open import Categories.Object.Initial using (IsInitial)
open import Function.Equality using (cong)
open import Categories.Morphism.Reasoning

open import F-Coalgebra-Colimit
open import Unchained-Utils


-- We first recap some lemmas from:
--   [CUV06] Venanzio Capretta, Tarmo Uustalu, and Varmo Vene.
--           Recursive coalgebras from comonads.
--           Inf. Comput., 204(4):437â€“468, 2006.

record Solution {o â„“ e} {C : Category o â„“ e} {F : Endofunctor C}
  (X : F-Coalgebra F)
  (Y : F-Algebra F) : Set (â„“ âŠ” e) where
  open Category C
  module X = F-Coalgebra X
  module Y = F-Algebra Y
  open Functor F
  field
    f : X.A â‡’ Y.A
    commutes : f â‰ˆ Y.Î± âˆ˜ Fâ‚ f âˆ˜ X.Î±

-- we can precompose solutions with coalgebra morphisms:
solution-precompose : {B D : F-Coalgebra F} â†’ {A : F-Algebra F} â†’
  Solution D A â†’ F-Coalgebra-Morphism B D â†’ Solution B A
solution-precompose {B} {D} {A} sol mor =
  let
    open Category C
    module sol = Solution sol
    module mor = F-Coalgebra-Morphism mor
    module B = F-Coalgebra B
    module D = F-Coalgebra D
    module A = F-Algebra A
    open HomReasoning
    open Functor F
  in
  record
  { f = sol.f âˆ˜ mor.f ;
  commutes = begin
    sol.f âˆ˜ mor.f                     â‰ˆâŸ¨ sol.commutes âŸ©âˆ˜âŸ¨refl  âŸ©
    (A.Î± âˆ˜ Fâ‚ sol.f âˆ˜ D.Î±) âˆ˜ mor.f     â‰ˆâŸ¨ assoc âŸ©
    A.Î± âˆ˜ (Fâ‚ sol.f âˆ˜ D.Î±) âˆ˜ mor.f     â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ assoc âŸ©
    A.Î± âˆ˜ Fâ‚ sol.f âˆ˜ D.Î± âˆ˜ mor.f       â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ reflâŸ©âˆ˜âŸ¨ mor.commutes âŸ©
    A.Î± âˆ˜ Fâ‚ sol.f âˆ˜ Fâ‚ mor.f âˆ˜ B.Î±    â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ sym-assoc âŸ©
    A.Î± âˆ˜ (Fâ‚ sol.f âˆ˜ Fâ‚ mor.f) âˆ˜ B.Î±  â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ homomorphism âŸ©âˆ˜âŸ¨refl âŸ©
    A.Î± âˆ˜ Fâ‚ (sol.f âˆ˜ mor.f) âˆ˜ B.Î±
          âˆ
  }

record IsRecursive (X : F-Coalgebra F) : Set (o âŠ” â„“ âŠ” e) where
  morph = Solution.f
  field
    -- there is at least one solution:
    recur : (B : F-Algebra F) â†’ Solution X B
    -- there is at most one solution:
    unique : (B : F-Algebra F) â†’ (g h : Solution X B) â†’
      C [ morph g â‰ˆ morph h ]


-- whenever a recursive coalgebra is an iso, it is the initial algebra:
-- [CUV06, Prop. 7(a)]
iso-recursiveâ‡’initial :
  (R : F-Coalgebra F)
  â†’ IsRecursive R
  â†’ (r-iso : IsIso C (F-Coalgebra.Î± R))
  â†’ IsInitial (F-Algebras F) (to-Algebra (IsIso.inv r-iso))
iso-recursiveâ‡’initial R is-rec r-iso =
  let
    open Category C
    open HomReasoning
    râ»Â¹ = IsIso.inv r-iso
    r = F-Coalgebra.Î± R
  in
  record
  { ! = Î» {A : F-Algebra F} â†’
      let
        coalg2alg = IsRecursive.recur is-rec A
        a = F-Algebra.Î± A
        h : (F-Coalgebra.A R) â‡’ (F-Algebra.A A)
        h = Solution.f coalg2alg
        Fh = Functor.Fâ‚ F h
      in
      record
        { f = h
        ; commutes = begin
          h âˆ˜ râ»Â¹
            â‰ˆâŸ¨  Solution.commutes coalg2alg âŸ©âˆ˜âŸ¨refl âŸ©
          (a âˆ˜ Fh âˆ˜ r) âˆ˜ râ»Â¹   â‰ˆâŸ¨ assoc âŸ©
          a âˆ˜ ((Fh âˆ˜ r) âˆ˜ râ»Â¹) â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ assoc âŸ©
          a âˆ˜ Fh âˆ˜ (r âˆ˜ râ»Â¹)
            â‰ˆË˜âŸ¨ assoc âŸ©
          (a âˆ˜ Fh) âˆ˜ (r âˆ˜ râ»Â¹)
            â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ Iso.isoÊ³ (IsIso.iso r-iso) âŸ©
          (a âˆ˜ Fh) âˆ˜ id
            â‰ˆâŸ¨ identityÊ³ âŸ©
          a âˆ˜ Fh
          âˆ
        }
  ; !-unique = Î» {A} g-hom â†’
    let
      g = (F-Algebra-Morphism.f g-hom)
      Fg = Functor.Fâ‚ F g
      a = F-Algebra.Î± A
      -- we first show that 'g' is a coalg2algebra homomorphism
      g-coalg2alg : Solution R A
      g-coalg2alg = record {
        f = g ;
        commutes =
          begin
          g          â‰ˆË˜âŸ¨ identityÊ³ âŸ©
          g âˆ˜ id      â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ Iso.isoË¡ (IsIso.iso r-iso) âŸ©
          g âˆ˜ (râ»Â¹ âˆ˜ r) â‰ˆË˜âŸ¨ assoc âŸ©
          (g âˆ˜ râ»Â¹) âˆ˜ r â‰ˆâŸ¨ F-Algebra-Morphism.commutes g-hom âŸ©âˆ˜âŸ¨refl âŸ©
          (a âˆ˜ Fg) âˆ˜ r â‰ˆâŸ¨ assoc âŸ©
          a âˆ˜ Fg âˆ˜ r
          âˆ
        }
      -- and thus, it has to be identical to h:
      h = IsRecursive.recur is-rec A
    in
    IsRecursive.unique is-rec A h g-coalg2alg
  }

-- Apply the functor F to the coalgebra carrier and structure:

module _ (R : F-Coalgebra F) (B : F-Coalgebra F) where
  -- ([CUV06, Prop. 5])
  open Category C
  private
    module R = F-Coalgebra R
    module B = F-Coalgebra B
    module F = Functor F

  sandwich-recursive :
    IsRecursive R
    â†’ (h : F-Coalgebra-Morphism R B)
    â†’ (g : F-Coalgebra-Morphism B (iterate R))
    â†’ B.Î± â‰ˆ F.Fâ‚ (F-Coalgebra-Morphism.f h) âˆ˜ (F-Coalgebra-Morphism.f g)
    â†’ IsRecursive B
  sandwich-recursive R-is-rec h g equation =
    let
      module h = F-Coalgebra-Morphism h
      module g = F-Coalgebra-Morphism g
      open IsRecursive R-is-rec
    in
    record {
    recur = Î» D â†’
      let
        -- for an F-algebra D, consider the induced solution by R:
        module D = F-Algebra D
        R2D = recur D
        module R2D = Solution R2D
        -- use this under the functor to get a solution from B to D:
        sol = D.Î± âˆ˜ F.Fâ‚ R2D.f âˆ˜ g.f
        open HomReasoning
      in
      record {
      f = sol;
      commutes =
          -- in the following, the only non-trivial steps are R2D.commutes and g.commutes
          begin
          sol                        â‰¡âŸ¨âŸ©
          D.Î± âˆ˜ F.Fâ‚ R2D.f âˆ˜ g.f      â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ F.F-resp-â‰ˆ R2D.commutes âŸ©âˆ˜âŸ¨refl âŸ©
          D.Î± âˆ˜ F.Fâ‚ (D.Î± âˆ˜ F.Fâ‚ R2D.f âˆ˜ R.Î±) âˆ˜ g.f          â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ F.F-resp-â‰ˆ assoc âŸ©âˆ˜âŸ¨refl âŸ©
          D.Î± âˆ˜ F.Fâ‚ ((D.Î± âˆ˜ F.Fâ‚ R2D.f) âˆ˜ R.Î±) âˆ˜ g.f        â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ F.homomorphism âŸ©âˆ˜âŸ¨refl âŸ©
          D.Î± âˆ˜ (F.Fâ‚ (D.Î± âˆ˜ F.Fâ‚ R2D.f) âˆ˜ F.Fâ‚ R.Î±) âˆ˜ g.f   â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ assoc âŸ©
          D.Î± âˆ˜ F.Fâ‚ (D.Î± âˆ˜ F.Fâ‚ R2D.f) âˆ˜ F.Fâ‚ R.Î± âˆ˜ g.f     â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ reflâŸ©âˆ˜âŸ¨ g.commutes âŸ©
          D.Î± âˆ˜ F.Fâ‚ (D.Î± âˆ˜ F.Fâ‚ R2D.f) âˆ˜ F.Fâ‚ g.f âˆ˜ B.Î±     â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ assoc âŸ©
          D.Î± âˆ˜ (F.Fâ‚ (D.Î± âˆ˜ F.Fâ‚ R2D.f) âˆ˜ F.Fâ‚ g.f) âˆ˜ B.Î±   â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ F.homomorphism âŸ©âˆ˜âŸ¨refl âŸ©
          D.Î± âˆ˜ F.Fâ‚ ((D.Î± âˆ˜ F.Fâ‚ R2D.f) âˆ˜ g.f) âˆ˜ B.Î±        â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ F.F-resp-â‰ˆ assoc âŸ©âˆ˜âŸ¨refl âŸ©
          D.Î± âˆ˜ F.Fâ‚ (D.Î± âˆ˜ F.Fâ‚ R2D.f âˆ˜ g.f) âˆ˜ B.Î±          â‰¡âŸ¨âŸ©
          D.Î± âˆ˜ F.Fâ‚ sol âˆ˜ B.Î±
          âˆ
      } ;
    unique = Î» D sol1 sol2 â†’
      let
        module D = F-Algebra D
        module sol1 = Solution sol1
        module sol2 = Solution sol2
        open HomReasoning
        -- first of all, the solutions are equal when precomposed with 'h: R -> B':
        sol1-h-is-sol2-h : sol1.f âˆ˜ h.f â‰ˆ sol2.f âˆ˜ h.f
        sol1-h-is-sol2-h =
          IsRecursive.unique R-is-rec D
             (solution-precompose sol1 h)
             (solution-precompose sol2 h)

        -- this is essentially the reasoning: we do it forward for sol1 and
        -- backwards for sol2.
        sol-transformation sol =
          let
            module sol = Solution sol
          in
          begin
          sol.f            â‰ˆâŸ¨ sol.commutes âŸ©
          D.Î± âˆ˜ F.Fâ‚ sol.f âˆ˜ B.Î±  â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ reflâŸ©âˆ˜âŸ¨ equation âŸ©
          D.Î± âˆ˜ F.Fâ‚ sol.f âˆ˜ F.Fâ‚ h.f âˆ˜ g.f  â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ sym-assoc âŸ©
          D.Î± âˆ˜ (F.Fâ‚ sol.f âˆ˜ F.Fâ‚ h.f) âˆ˜ g.f  â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ F.homomorphism âŸ©âˆ˜âŸ¨refl âŸ©
          D.Î± âˆ˜ F.Fâ‚ (sol.f âˆ˜ h.f) âˆ˜ g.f
          âˆ
      in
      begin
      sol1.f            â‰ˆâŸ¨ sol-transformation sol1 âŸ©
      D.Î± âˆ˜ F.Fâ‚ (sol1.f âˆ˜ h.f) âˆ˜ g.f   â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ F.F-resp-â‰ˆ sol1-h-is-sol2-h âŸ©âˆ˜âŸ¨refl âŸ©
      D.Î± âˆ˜ F.Fâ‚ (sol2.f âˆ˜ h.f) âˆ˜ g.f  â‰ˆË˜âŸ¨ sol-transformation sol2 âŸ©
      sol2.f
      âˆ
    }


-- Corollary: If (R,r) is recursive, then so is (FR,Fr) ([CUV06, Prop. 6]).
iterate-recursive : (R : F-Coalgebra F)
                    â†’ IsRecursive R
                    â†’ IsRecursive (iterate R)
iterate-recursive R is-rec =
  let
    module R = F-Coalgebra R
    g : F-Coalgebra-Morphism R (iterate R)
    g =
      let
        open Category C
        open Equiv
      in
      record { f = R.Î± ; commutes = refl }

    equation =
      let
        module FR = F-Coalgebra (iterate R)
        open Functor F
        open Category C
        open HomReasoning
      in
      begin
      FR.Î± â‰ˆË˜âŸ¨ identityÊ³ âŸ©
      Fâ‚ R.Î± âˆ˜ id
      âˆ

    open Category (F-Coalgebras F)
  in
  sandwich-recursive R (iterate R) is-rec g id equation

-- the functor sends coalgebra morphisms to coalgebra morphisms:
iterate-F-Coalgebra-Morphism : {A B : F-Coalgebra F}
  â†’ (h : F-Coalgebra-Morphism A B)
  â†’ F-Coalgebra-Morphism (iterate A) (iterate B)
iterate-F-Coalgebra-Morphism {A} {B} h =
  record {
    f = F.â‚ h.f ; commutes = begin
      F.â‚ Î² âˆ˜ F.â‚ h.f â‰ˆË˜âŸ¨ F.homomorphism âŸ©
      F.â‚ (Î² âˆ˜ h.f) â‰ˆâŸ¨ F.F-resp-â‰ˆ h.commutes âŸ©
      F.â‚ (F.â‚ h.f âˆ˜ Î±) â‰ˆâŸ¨ F.homomorphism âŸ©
      F.â‚ (F.â‚ h.f) âˆ˜ F.â‚ Î±
      âˆ}
  where
    module h = F-Coalgebra-Morphism h
    open F-Coalgebra A
    open F-Coalgebra B renaming (A to B; Î± to Î²)
    module F = Functor F
    open Category C
    open HomReasoning


record R-Coalgebra : Set (o âŠ” â„“ âŠ” e) where
  field
    coalg : F-Coalgebra F
    ump : IsRecursive coalg
  open F-Coalgebra coalg public
  open IsRecursive ump public

-- The recursive coalgebras form a full subcategory of F-Coalgebras:
R-Coalgebras : Category (â„“ âŠ” o âŠ” e) (e âŠ” â„“) e
R-Coalgebras = FullSubCategory (F-Coalgebras F) R-Coalgebra.coalg
  where
    open import Categories.Category.SubCategory using (FullSubCategory)


module _ where

  open import Categories.Functor.Construction.SubCategory
  forget-rec : Functor (R-Coalgebras) (F-Coalgebras F)
  forget-rec = FullSub (F-Coalgebras F)

open import Categories.Diagram.Colimit using (Colimit)
open import Categories.Diagram.Cocone
open import Categories.Functor using (_âˆ˜F_)

module _ {o' â„“' e' : Level} {ğ’Ÿ : Category o' â„“' e'} (J : Functor ğ’Ÿ (F-Coalgebras F)) where
  private
    module ğ’Ÿ = Category ğ’Ÿ
    module J = Functor J
    module F = Functor F

  Colimit-IsRecursive : (âˆ€ (i : ğ’Ÿ.Obj) â†’ IsRecursive (J.â‚€ i))
      â†’ (colim : Colimit (forget-Coalgebra âˆ˜F J))
      â†’ IsRecursive (Colimit.coapex (F-Coalgebras-Colimit J colim))
  Colimit-IsRecursive all-recursive obj-colim =
    record { recur = Î» B â†’ coconeâ‡’-to-sol B (obj-colim.rep-cocone (alg2cocone B))
           ; unique = Î» B g h â†’ obj-colim.initial.!-uniqueâ‚‚ (sol-to-coconeâ‡’ B g) (sol-to-coconeâ‡’ B h)
           }
    where
      open Category C
      open HomReasoning
      colim : Colimit J
      colim = F-Coalgebras-Colimit J obj-colim
      module colim = Colimit colim
      module obj-colim = Colimit obj-colim
      alg2cocone : F-Algebra F â†’ Cocone (forget-Coalgebra âˆ˜F J)
      alg2cocone B =
        let module B = F-Algebra B in
        record { coapex = record {
          Ïˆ = Î» i â†’ Solution.f (IsRecursive.recur (all-recursive i) B) ;
          commute = Î» {i} {i'} h â†’
            let
              sol1 = IsRecursive.recur (all-recursive i) B
              sol2 = solution-precompose (IsRecursive.recur (all-recursive i') B) (J.â‚ h)
            in
            IsRecursive.unique (all-recursive i) B sol2 sol1 } }
      coconeâ‡’-to-sol : (B : F-Algebra F)
                  â†’ Coconeâ‡’ (forget-Coalgebra âˆ˜F J) obj-colim.colimit (alg2cocone B)
                  â†’ Solution colim.coapex B
      coconeâ‡’-to-sol B mor = let
          module B = F-Algebra B
          module mor = Coconeâ‡’ mor
        in
        record { f = mor.arr ; commutes = colimit-is-jointly-epic obj-colim (Î» i â†’
          let
            sol = IsRecursive.recur (all-recursive i) B
            module sol = Solution sol
          in
          begin
          mor.arr âˆ˜ obj-colim.proj i â‰ˆâŸ¨ mor.commute {i} âŸ©
          sol.f  â‰ˆâŸ¨ sol.commutes âŸ©
          B.Î± âˆ˜ F.Fâ‚ sol.f âˆ˜ F-Coalgebra.Î± (J.â‚€ i)  â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ F.F-resp-â‰ˆ mor.commute âŸ©âˆ˜âŸ¨refl âŸ©
          B.Î± âˆ˜ F.Fâ‚ (mor.arr âˆ˜ obj-colim.proj i) âˆ˜ F-Coalgebra.Î± (J.â‚€ i)  â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ pushË¡ C F.homomorphism âŸ©
          B.Î± âˆ˜ F.Fâ‚ mor.arr âˆ˜ F.â‚ (obj-colim.proj i) âˆ˜ F-Coalgebra.Î± (J.â‚€ i)  â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ reflâŸ©âˆ˜âŸ¨ F-Coalgebra-Morphism.commutes (colim.proj i) âŸ©
          B.Î± âˆ˜ F.Fâ‚ mor.arr âˆ˜ F-Coalgebra.Î± colim.coapex âˆ˜ obj-colim.proj i  â‰ˆË˜âŸ¨ (assoc â—‹ (reflâŸ©âˆ˜âŸ¨ assoc)) âŸ©
          (B.Î± âˆ˜ F.Fâ‚ mor.arr âˆ˜ F-Coalgebra.Î± colim.coapex) âˆ˜ obj-colim.proj i
          âˆ) }
      sol-to-coconeâ‡’ : (B : F-Algebra F) â†’ Solution colim.coapex B
                  â†’ Coconeâ‡’ (forget-Coalgebra âˆ˜F J) obj-colim.colimit (alg2cocone B)
      sol-to-coconeâ‡’ B sol = let
          module B = F-Algebra B
          module sol = Solution sol
        in record
        { arr = sol.f
        ; commute = Î» {i} â†’
            IsRecursive.unique (all-recursive i) B
            (solution-precompose sol (colim.proj i))
            (IsRecursive.recur (all-recursive i) B)
        }


R-Coalgebras-Colimit : {o' â„“' e' : Level} â†’ {D : Category o' â„“' e'} â†’ (J : Functor D R-Coalgebras)
        â†’ Colimit (forget-Coalgebra âˆ˜F forget-rec âˆ˜F  J) â†’ Colimit J
R-Coalgebras-Colimit J C-colim =
  FullSub-Colimit R-Coalgebra.coalg J Coalg-colim R (â‰….refl (F-Coalgebras F))
  where
    module J = Functor J
    module C-colim = Colimit C-colim
    module F = Functor F
    Coalg-colim : Colimit (forget-rec âˆ˜F J)
    Coalg-colim = F-Coalgebras-Colimit _ C-colim
    module Coalg-colim = Colimit Coalg-colim

    -- every F-Algebra induces a competing cocone for the above colimit:
    alg2cocone : F-Algebra F â†’ Cocone (forget-Coalgebra âˆ˜F forget-rec âˆ˜F  J)
    alg2cocone B =
      let
            module B = F-Algebra B
      in
      record {
        N = B.A ;
        coapex = record {
          Ïˆ = Î» R â†’
            let
              module R = R-Coalgebra (J.Fâ‚€ R)
            in
            Solution.f (R.recur B) ;
          commute = Î» {R} {R'} h â†’
            let
              -- h is a coalg-hom from R to R':
              module R = R-Coalgebra (J.Fâ‚€ R)
              module R' = R-Coalgebra (J.Fâ‚€ R')
              open Category C
              open HomReasoning
              open Equiv
              module h = F-Coalgebra-Morphism (J.Fâ‚ h)
              module U = Functor (forget-Coalgebra âˆ˜F forget-rec âˆ˜F J)
              module U' = Functor (forget-rec âˆ˜F J)
              -- we can use it to construct another solution from R to B:
              sol : Solution R.coalg B
              sol =
                let
                  module r' = Solution (R'.recur B)
                in
                record {
                f = r'.f âˆ˜ U.Fâ‚ h;
                commutes =
                begin
                r'.f âˆ˜ U.Fâ‚ h â‰ˆâŸ¨ r'.commutes âŸ©âˆ˜âŸ¨refl âŸ©
                (B.Î± âˆ˜ (F.Fâ‚ r'.f âˆ˜ R'.Î±)) âˆ˜ U.Fâ‚ h â‰ˆâŸ¨ assoc âŸ©
                B.Î± âˆ˜ ((F.Fâ‚ r'.f âˆ˜ R'.Î±) âˆ˜ U.Fâ‚ h) â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ assoc âŸ©
                B.Î± âˆ˜ (F.Fâ‚ r'.f âˆ˜ (R'.Î± âˆ˜ U.Fâ‚ h)) â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ reflâŸ©âˆ˜âŸ¨ h.commutes âŸ©
                B.Î± âˆ˜ (F.Fâ‚ r'.f âˆ˜ (F.Fâ‚ (U.Fâ‚ h) âˆ˜ R.Î±)) â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ sym-assoc âŸ©
                B.Î± âˆ˜ ((F.Fâ‚ r'.f âˆ˜ F.Fâ‚ (U.Fâ‚ h)) âˆ˜ R.Î±) â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ F.homomorphism âŸ©âˆ˜âŸ¨refl âŸ©
                B.Î± âˆ˜ F.Fâ‚ (r'.f âˆ˜ U.Fâ‚ h) âˆ˜ R.Î±
                âˆ
                }
            in
            R.unique B sol (R.recur B)
        } }
    --
    -- the induced solution for an algebra
    alg2solution : (B : F-Algebra F) â†’ Solution Coalg-colim.coapex B
    alg2solution B =
      let
        module B = F-Algebra B
        open Category C
        open HomReasoning

        sol : C [ F-Coalgebra.A Coalg-colim.coapex , B.A ]
        sol = C-colim.rep (alg2cocone B)
      in
      record { f = sol ;
        commutes = colimit-is-jointly-epic C-colim Î» R â†’
            let
              module R = R-Coalgebra (J.Fâ‚€ R)
              module R-sol = Solution (R.recur B)
            in
            begin
            sol âˆ˜ C-colim.proj R
              â‰ˆâŸ¨ C-colim.commute âŸ©
            R-sol.f
              â‰ˆâŸ¨ R-sol.commutes âŸ©
            B.Î± âˆ˜ F.Fâ‚ R-sol.f âˆ˜ R.Î±
              â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ F.F-resp-â‰ˆ C-colim.commute âŸ©âˆ˜âŸ¨refl âŸ©
            B.Î± âˆ˜ F.Fâ‚ (sol âˆ˜ C-colim.proj R) âˆ˜ R.Î±
              â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ F.homomorphism âŸ©âˆ˜âŸ¨refl âŸ©
            B.Î± âˆ˜ (F.Fâ‚ sol âˆ˜ F.Fâ‚ (C-colim.proj R)) âˆ˜ R.Î±
              â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ assoc âŸ©
            B.Î± âˆ˜ F.Fâ‚ sol âˆ˜ (F.Fâ‚ (C-colim.proj R) âˆ˜ R.Î±)
              â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ reflâŸ©âˆ˜âŸ¨ F-Coalgebra-Morphism.commutes (Coalg-colim.proj R) âŸ©
            B.Î± âˆ˜ F.Fâ‚ sol âˆ˜ F-Coalgebra.Î± Coalg-colim.coapex âˆ˜ C-colim.proj R
              â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨  assoc âŸ©
            B.Î± âˆ˜ (F.Fâ‚ sol âˆ˜ F-Coalgebra.Î± Coalg-colim.coapex) âˆ˜ C-colim.proj R
              â‰ˆË˜âŸ¨  assoc âŸ©
            (B.Î± âˆ˜ F.Fâ‚ sol âˆ˜ F-Coalgebra.Î± Coalg-colim.coapex) âˆ˜ C-colim.proj R
            âˆ
          }

    -- we can then show that the colimit coalgebra must be recursive:
    R : R-Coalgebra
    R = record {
      coalg = Coalg-colim.coapex ;
      ump = record {
        recur = alg2solution;
        unique = Î» B g h â†’
          colimit-is-jointly-epic C-colim Î» R â†’
            let
              open Category C
              open HomReasoning
              module B = F-Algebra B
              module R = R-Coalgebra (J.Fâ‚€ R)
              -- we need to show that every solution in the colim induces a solution of R
              proj-sol : Solution Coalg-colim.coapex B â†’ Solution R.coalg B
              proj-sol s =
                let module s = Solution s in
                record {
                f = s.f âˆ˜ C-colim.proj R ;
                commutes =
                  begin
                  s.f âˆ˜ C-colim.proj R
                    â‰ˆâŸ¨ s.commutes âŸ©âˆ˜âŸ¨refl âŸ©
                  (B.Î± âˆ˜ F.Fâ‚ s.f âˆ˜ F-Coalgebra.Î± Coalg-colim.coapex) âˆ˜ C-colim.proj R
                    â‰ˆâŸ¨ assoc âŸ©
                  B.Î± âˆ˜ ((F.Fâ‚ s.f âˆ˜ F-Coalgebra.Î± Coalg-colim.coapex) âˆ˜ C-colim.proj R)
                    â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ assoc âŸ©
                  B.Î± âˆ˜ F.Fâ‚ s.f âˆ˜ F-Coalgebra.Î± Coalg-colim.coapex âˆ˜ C-colim.proj R
                    â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ reflâŸ©âˆ˜âŸ¨ F-Coalgebra-Morphism.commutes (Coalg-colim.proj R) âŸ©
                  B.Î± âˆ˜ F.Fâ‚ s.f âˆ˜ F.Fâ‚ (C-colim.proj R) âˆ˜ R.Î±
                    â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ assoc âŸ©
                  B.Î± âˆ˜ (F.Fâ‚ s.f âˆ˜ F.Fâ‚ (C-colim.proj R)) âˆ˜ R.Î±
                    â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ F.homomorphism âŸ©âˆ˜âŸ¨refl âŸ©
                  B.Î± âˆ˜ F.Fâ‚ (s.f âˆ˜ C-colim.proj R) âˆ˜ R.Î±
                  âˆ
                }
            in
            R.unique B (proj-sol g) (proj-sol h)
        } }


retract-coalgebra : (X : F-Coalgebra F) {Y : C.Obj}
  â†’ Retract C (F-Coalgebra.A X) Y
  â†’ F-Coalgebra F
retract-coalgebra X {Y} r = record { A = Y ; Î± = Fâ‚ r.section âˆ˜ X.Î± âˆ˜ r.retract }
  where
    open Functor F
    open Category C
    module r = Retract r
    module X = F-Coalgebra X


retract-coalgebra-hom : (X : F-Coalgebra F) {Y : C.Obj}
  â†’ (r : Retract C (F-Coalgebra.A X) Y)
  â†’ F-Coalgebras F [ X , retract-coalgebra X r ]
retract-coalgebra-hom X {Y} r =
  record { f = r.section ; commutes = begin
    (Fâ‚ r.section âˆ˜ X.Î± âˆ˜ r.retract) âˆ˜ r.section â‰ˆâŸ¨ assocÂ²' C âŸ©
    Fâ‚ r.section âˆ˜ X.Î± âˆ˜ r.retract âˆ˜ r.section â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ elimÊ³ C r.is-retract âŸ©
    Fâ‚ r.section âˆ˜ X.Î±
    âˆ}
  where
    open Functor F
    open Category C
    open HomReasoning
    module r = Retract r
    module X = F-Coalgebra X

retract-coalgebra-homâ»Â¹ : (X : F-Coalgebra F) {Y : C.Obj}
  â†’ (r : Retract C (F-Coalgebra.A X) Y)
  â†’ F-Coalgebras F [ retract-coalgebra X r , X ]
retract-coalgebra-homâ»Â¹ X {Y} r =
  record { f = r.retract ; commutes = begin
    X.Î± âˆ˜ r.retract â‰ˆË˜âŸ¨ pullË¡ C (elimË¡ C (F-resp-â‰ˆ r.is-retract â—‹ identity)) âŸ©
    Fâ‚ (r.retract âˆ˜ r.section) âˆ˜ X.Î± âˆ˜ r.retract â‰ˆâŸ¨ pushË¡ C homomorphism âŸ©
    Fâ‚ r.retract âˆ˜ Fâ‚ r.section âˆ˜ X.Î± âˆ˜ r.retract
    âˆ}
  where
    open Functor F
    open Category C
    open HomReasoning
    module r = Retract r
    module X = F-Coalgebra X

retract-coalgebra-hom-to-iterate : (X : F-Coalgebra F) {Y : C.Obj}
  â†’ (r : Retract C (F-Coalgebra.A X) Y)
  â†’ F-Coalgebras F [ retract-coalgebra X r , (iterate X) ]
retract-coalgebra-hom-to-iterate X {Y} r =
  record { f = X.Î± âˆ˜ r.retract ; commutes =
    begin
    Fâ‚ X.Î± âˆ˜ X.Î± âˆ˜ r.retract â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ elimË¡ C identity âŸ©
    Fâ‚ X.Î± âˆ˜ Fâ‚ id âˆ˜ X.Î± âˆ˜ r.retract â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ F-resp-â‰ˆ r.is-retract âŸ©âˆ˜âŸ¨refl  âŸ©
    Fâ‚ X.Î± âˆ˜ Fâ‚ (r.retract âˆ˜ r.section) âˆ˜ X.Î± âˆ˜ r.retract â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ pushË¡ C homomorphism âŸ©
    Fâ‚ X.Î± âˆ˜ Fâ‚ r.retract âˆ˜ Fâ‚ r.section âˆ˜ X.Î± âˆ˜ r.retract â‰ˆË˜âŸ¨ pushË¡ C homomorphism âŸ©
    Fâ‚ (X.Î± âˆ˜ r.retract) âˆ˜ Fâ‚ r.section âˆ˜ X.Î± âˆ˜ r.retract
    âˆ
  }
  where
    open Functor F
    open Category C
    open HomReasoning
    module r = Retract r
    module X = F-Coalgebra X

retract-coalgebra-recursive : (X : F-Coalgebra F) {Y : C.Obj}
  â†’ (r : Retract C (F-Coalgebra.A X) Y)
  â†’ IsRecursive X
  â†’ IsRecursive (retract-coalgebra X r)
retract-coalgebra-recursive X {Y} r X-rec =
  sandwich-recursive X (retract-coalgebra X r) X-rec
    (retract-coalgebra-hom X r)
    (retract-coalgebra-hom-to-iterate X r) C.Equiv.refl
  where
    open Functor F
    open Category C
    open HomReasoning
    module r = Retract r
    module X = F-Coalgebra X
